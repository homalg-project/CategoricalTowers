# SPDX-License-Identifier: GPL-2.0-or-later
# Toposes: Elementary toposes
#
# Implementations
#

##
AddDerivationToCAP( IndexOfNonliftableMorphismFromDistinguishedObject,
        "IndexOfNonliftableMorphismFromDistinguishedObject as the index of the first nonliftable morphism in ExactCoverWithGlobalElements",
        [ [ ExactCoverWithGlobalElements, 1 ],
          [ IsLiftableAlongMonomorphism, 2 ],
          [ ObjectDatum, 1 ] ],
        
  function( cat, iota )
    local target, global_morphisms;
    
    target := Target( iota );
    
    global_morphisms := ExactCoverWithGlobalElements( cat, target );
    
    ## start interval at 0 to unify ranges for the compiler
    return 1 + SafeFirst( [ 0 .. ObjectDatum( cat, target ) - 1 ],
                   i -> not IsLiftableAlongMonomorphism( cat, iota, global_morphisms[1 + i] ) );
    
end : CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
CategoryFilter := function( cat )
    return HasRangeCategoryOfHomomorphismStructure( cat ) and
           IsIdenticalObj( cat, RangeCategoryOfHomomorphismStructure( cat ) );
end );

##
AddDerivationToCAP( NonliftableMorphismFromDistinguishedObject,
        "",
        [ [ ExactCoverWithGlobalElements, 1 ],
          [ IndexOfNonliftableMorphismFromDistinguishedObject, 1 ] ],
        
  function( cat, iota )
    local global_morphisms, index;
    
    global_morphisms := ExactCoverWithGlobalElements( cat, Target( iota ) );
    
    index := IndexOfNonliftableMorphismFromDistinguishedObject( cat, iota );
    
    return global_morphisms[index];
    
end : CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
CategoryFilter := function( cat )
    return HasRangeCategoryOfHomomorphismStructure( cat ) and
           IsIdenticalObj( cat, RangeCategoryOfHomomorphismStructure( cat ) );
end );

##
AddDerivationToCAP( InjectionOfCoproductComplement,
        "InjectionOfCoproductComplement by iteratively calling NonliftableMorphismFromDistinguishedObject",
        [ [ DistinguishedObjectOfHomomorphismStructure, 1 ],
          [ ObjectDatum, 2 ],
          [ NonliftableMorphismFromDistinguishedObject, 2 ],
          [ UniversalMorphismFromCoproduct, 4 ],
          [ UniversalMorphismFromInitialObject, 1 ] ],
        
  function( cat, iota )
    local source, target, s, t, initial_complement, distinguished_object, predicate, func, initial;
    
    source := Source( iota );
    
    target := Target( iota );
    
    s := ObjectDatum( cat, source );
    
    t := ObjectDatum( cat, target );
    
    initial_complement := UniversalMorphismFromInitialObject( cat, target );
    
    if s = t then
        return initial_complement;
    fi;
    
    distinguished_object := DistinguishedObjectOfHomomorphismStructure( cat );
    
    predicate :=
      function( data_old, data_new )
        
        return data_new[3] = 0;
        
    end;
    
    func :=
      function( complement_coproduct_index )
        local complement, coproduct, index, nonliftable, coproduct_new, complement_new;
        
        complement := complement_coproduct_index[1];
        coproduct := complement_coproduct_index[2];
        index := complement_coproduct_index[3];
        
        nonliftable := NonliftableMorphismFromDistinguishedObject( cat,
                               coproduct );
        
        coproduct_new := UniversalMorphismFromCoproduct( cat,
                                 [ Source( coproduct ), distinguished_object ],
                                 target,
                                 [ coproduct, nonliftable ] );
        
        complement_new := UniversalMorphismFromCoproduct( cat,
                                  [ Source( complement ), distinguished_object ],
                                  target,
                                  [ complement, nonliftable ] );
        
        return Triple( complement_new,
                       coproduct_new,
                       index - 1 );
        
    end;
    
    initial := Triple( initial_complement,
                       iota,
                       t - s );
    
    return CapFixpoint( predicate, func, initial )[1];
    
end : CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
CategoryFilter := function( cat )
    return HasRangeCategoryOfHomomorphismStructure( cat ) and
           IsIdenticalObj( cat, RangeCategoryOfHomomorphismStructure( cat ) );
end );

##
AddDerivationToCAP( CoproductComplement,
        "CoproductComplement as the source of InjectionOfCoproductComplement",
        [ [ InjectionOfCoproductComplement, 1 ] ],
        
  function( cat, mor )
    
    return Source( InjectionOfCoproductComplement( cat, mor ) );
    
end );

##
AddDerivationToCAP( DirectProductComplement,
        "DirectProductComplement as the target of ProjectionInDirectProductComplement",
        [ [ ProjectionInDirectProductComplement, 1 ] ],
        
  function( cat, mor )
    
    return Target( ProjectionInDirectProductComplement( cat, mor ) );
    
end );

## Page 20 in Peter Freyd, Aspect of topoi, Bull. Austral. Math. Soc, 7 (1972)
AddDerivationToCAP( ImageEmbedding,
        "the (regular) image as the equalizer of the cokernel-pair",
        [ [ EmbeddingOfEqualizer, 1 ],
          [ InjectionOfCofactorOfPushout, 2 ] ],
        
  function( cat, mor )
    local D;
    
    D := [ mor, mor ];
    
    return EmbeddingOfEqualizer( cat,
                   [ InjectionOfCofactorOfPushout( cat, D, 1 ),
                     InjectionOfCofactorOfPushout( cat, D, 2 ) ] );
    
end );

##
AddDerivationToCAP( ImageEmbedding,
        "ImageEmbedding as the colift along the coastriction to image",
        [ [ CoastrictionToImage, 1 ],
          [ ColiftAlongEpimorphism, 1 ] ],
        
  function( cat, mor )
    
    return ColiftAlongEpimorphism( cat,
                   CoastrictionToImage( cat, mor ),
                   mor );
    
end : CategoryFilter := IsElementaryTopos );

##
AddDerivationToCAP( ImageEmbeddingWithGivenImageObject,
        "ImageEmbeddingWithGivenImageObject as the colift along the coastriction to image",
        [ [ CoastrictionToImageWithGivenImageObject, 1 ],
          [ ColiftAlongEpimorphism, 1 ] ],
        
  function( cat, mor, image_object )
    
    return ColiftAlongEpimorphism( cat,
                   CoastrictionToImageWithGivenImageObject( cat, mor, image_object ),
                   mor );
    
end : CategoryFilter := IsElementaryTopos );

##
AddDerivationToCAP( CoimageProjection,
        "CoimageProjection as the lift along the astriction to coimage",
        [ [ AstrictionToCoimage, 1 ],
          [ LiftAlongMonomorphism, 1 ] ],
        
  function ( cat, mor )
    
    return LiftAlongMonomorphism( cat,
                   AstrictionToCoimage( cat, mor ),
                   mor );
    
end : CategoryFilter := IsElementaryTopos );

##
AddDerivationToCAP( CoimageProjectionWithGivenCoimageObject,
        "CoimageProjectionWithGivenCoimageObject as the lift along the astriction to coimage",
        [ [ AstrictionToCoimageWithGivenCoimageObject, 1 ],
          [ LiftAlongMonomorphism, 1 ] ],
        
  function ( cat, mor, coimage_object )
    
    return LiftAlongMonomorphism( cat,
                   AstrictionToCoimageWithGivenCoimageObject( cat, mor, coimage_object ),
                   mor );
    
end : CategoryFilter := IsElementaryTopos );

##
AddDerivationToCAP( SubobjectOfClassifyingMorphism,
        "SubobjectOfClassifyingMorphism using the fiber product along the true morphism",
        [ [ TruthMorphismOfTrueWithGivenObjects, 1 ],
          [ TerminalObject, 1 ],
          [ SubobjectClassifier, 1 ],
          [ ProjectionInFactorOfFiberProduct, 1 ],
          [ IsMonomorphism, 1 ] ],
        
  function( cat, mor )
    local truth, subobject;
    
    truth := TruthMorphismOfTrueWithGivenObjects( cat,
                     TerminalObject( cat ),
                     SubobjectClassifier( cat ) );
    
    subobject := ProjectionInFactorOfFiberProduct( cat, [ mor, truth ], 1 );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    Assert( 4, IsMonomorphism( subobject ) );
    #% CAP_JIT_DROP_NEXT_STATEMENT
    SetIsMonomorphism( subobject, true );
    
    return subobject;
    
end );

##
AddDerivationToCAP( CartesianSquareOfSubobjectClassifier,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ DirectProduct, 1 ] ],
        
  function( cat )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return DirectProduct( cat, [ Omega, Omega ] );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Exercise 4.2.1]
## âŠ¤: ðŸ™ â†ª Î© classfies id_ðŸ™: ðŸ™ â†ª ðŸ™
AddDerivationToCAP( TruthMorphismOfTrueWithGivenObjects,
        "",
        [ [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ IdentityMorphism, 1 ] ],
        
  function( cat, T, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   IdentityMorphism( cat, T ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 5.4 (False)]
## âŠ¥: ðŸ™ â†ª Î© classifies âˆ… â†ª ðŸ™
AddDerivationToCAP( TruthMorphismOfFalseWithGivenObjects,
        "",
        [ [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ UniversalMorphismIntoTerminalObjectWithGivenTerminalObject, 1 ],
          [ InitialObject, 1 ] ],
        
  function( cat, T, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat, InitialObject( cat ), T ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## Â¬: Î© â†’ Î© classifies âŠ¥: ðŸ™ â†ª Î©
AddDerivationToCAP( TruthMorphismOfNotWithGivenObjects,
        "",
        [ [ TerminalObject, 1 ],
          [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ TruthMorphismOfFalseWithGivenObjects, 1 ] ],
        
  function( cat, Omega, Omega1 )
    local T;
    
    T := TerminalObject( cat );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   TruthMorphismOfFalseWithGivenObjects( cat, T, Omega ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## âˆ§: Î© Ã— Î© â†’ Î© classifies the product morphism âŸ¨ âŠ¤, âŠ¤ âŸ©: ðŸ™ â†ª Î© Ã— Î© of twice the morphism âŠ¤: ðŸ™ â†ª Î©
AddDerivationToCAP( TruthMorphismOfAndWithGivenObjects,
        "",
        [ [ TerminalObject, 1 ],
          [ TruthMorphismOfTrueWithGivenObjects, 1 ],
          [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ UniversalMorphismIntoDirectProductWithGivenDirectProduct, 1 ] ],
        
  function( cat, Omega2, Omega )
    local T, t;
    
    T := TerminalObject( cat );
    
    t := TruthMorphismOfTrueWithGivenObjects( cat, T, Omega );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   UniversalMorphismIntoDirectProductWithGivenDirectProduct( cat,
                           [ Omega, Omega ],
                           T,
                           [ t, t ],
                           Omega2 ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## âˆ¨: Î© Ã— Î© â†’ Î© classifies [ âŸ¨âŠ¤_Î©,id_Î©âŸ©, âŸ¨id_Î©,âŠ¤_Î©âŸ© ]: Î© âŠ” Î© â†ª Î© Ã— Î©
AddDerivationToCAP( TruthMorphismOfOrWithGivenObjects,
        "",
        [ [ TerminalObject, 1 ],
          [ PreCompose, 1 ],
          [ UniversalMorphismIntoTerminalObjectWithGivenTerminalObject, 1 ],
          [ TruthMorphismOfTrueWithGivenObjects, 1 ],
          [ IdentityMorphism, 1 ],
          [ UniversalMorphismIntoDirectProductWithGivenDirectProduct, 2 ],
          [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ UniversalMorphismFromCoproduct, 1 ] ],
        
  function( cat, Omega2, Omega )
    local T, t, id, left, right;
    
    T := TerminalObject( cat );
    
    ## âŠ¤_Î©: Î© â†’ ðŸ™ â†’ Î© is the morphism classifying the full subobject of Î©, i.e.,
    ## âŠ¤_Î© = ClassifyingMorphismOfSubobject( IdentityMorphism( Omega ) )
    t := PreCompose( cat,
                 UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat, Omega, T ),
                 TruthMorphismOfTrueWithGivenObjects( cat, T, Omega ) );
    
    id := IdentityMorphism( cat, Omega );
    
    left := UniversalMorphismIntoDirectProductWithGivenDirectProduct( cat,
                    [ Omega, Omega ],
                    Omega,
                    [ t, id ],
                    Omega2 );
    
    right := UniversalMorphismIntoDirectProductWithGivenDirectProduct( cat,
                     [ Omega, Omega ],
                     Omega,
                     [ id, t ],
                     Omega2 );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   UniversalMorphismFromCoproduct( cat,
                           [ Omega, Omega ],
                           Omega2,
                           [ left, right ] ),
                   Omega );
    
end );

## â‡’: Î© Ã— Î© â†’ Î© classifies the equalizer embedding E â†ª Î© Ã— Î© of (âˆ§: Î© Ã— Î© â†’ Î©, Ï€_1: Î© Ã— Î© â†’ Î©)
AddDerivationToCAP( TruthMorphismOfImpliesWithGivenObjects,
        "",
        [ [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ EmbeddingOfEqualizer, 1 ],
          [ TruthMorphismOfAndWithGivenObjects, 1 ],
          [ ProjectionInFactorOfDirectProductWithGivenDirectProduct, 1 ] ],
        
  function( cat, Omega2, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier( cat,
                   EmbeddingOfEqualizer( cat,
                           Omega2,
                           [ TruthMorphismOfAndWithGivenObjects( cat,
                                   Omega2,
                                   Omega ),
                             ProjectionInFactorOfDirectProductWithGivenDirectProduct( cat,
                                     [ Omega, Omega ],
                                     1,
                                     Omega2 ) ] ),
                   Omega );
    
end );

##
AddDerivationToCAP( PowerObject,
        "",
        [ [ ExponentialOnObjects, 1 ],
          [ SubobjectClassifier, 1 ] ],
        
  function( cat, a )
    
    return ExponentialOnObjects( cat, a, SubobjectClassifier( cat ) );
    
end );

## f: a â†’ b â‡ P(f): P(b) â†’ P(a)
AddDerivationToCAP( PowerObjectFunctorialWithGivenPowerObjects,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ IdentityMorphism, 1 ],
          [ ExponentialOnMorphismsWithGivenExponentials, 1 ] ],
        
  function( cat, Pb, f, Pa )
    
    return ExponentialOnMorphismsWithGivenExponentials( cat, Pb, f, IdentityMorphism( cat, SubobjectClassifier( cat ) ), Pa );
    
end );

## P(a) Ã— a â†’ Î©
AddDerivationToCAP( PowerObjectEvaluationMorphismWithGivenObjects,
        "PowerObjectEvaluationMorphismWithGivenObjects as a special case of the cartesian evaluation",
        [ [ CartesianLeftEvaluationMorphismWithGivenSource, 1 ] ],
        
  function( cat, Pa_xa, a, Omega )
    
    return CartesianLeftEvaluationMorphismWithGivenSource( cat,
                   a,
                   Omega,
                   Pa_xa );
    
end );

## (f:a Ã— b â†’ Î©) â†¦ (a â†’ P(b))
AddDerivationToCAP( PTransposeMorphismWithGivenRange,
        "PTransposeMorphismWithGivenRange as a special case of the cartesian adjunction",
        [ [ DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential, 1 ] ],
        
  function( cat, a, b, f, Pb )
    
    return DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential( cat,
                   a,
                   b,
                   f,
                   Pb );
    
end );

## Rewrite a relation Î¼:R â†ª a Ã— b as a morphism a â†’ P(b)
AddDerivationToCAP( UpperSegmentOfRelationWithGivenRange,
        "",
        [ [ ClassifyingMorphismOfSubobject, 1 ],
          [ DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential, 1 ] ],
        
  function( C, a, b, mu, Pb )
    local chi;
    
    ## Ï‡: a Ã— b â†’ Î© is the classifying morphism of Î¼: s(Î¼) â†ª a Ã— b
    chi := ClassifyingMorphismOfSubobject( C, mu );
    
    ## a â†’ P(b) encoding the relation given by Î¼
    return DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential( C, a, b, chi, Pb );
    
end );

## Rewrite a relation Î¼:R â†ª a Ã— b as a morphism b â†’ P(a)
AddDerivationToCAP( LowerSegmentOfRelationWithGivenRange,
        "",
        [ [ CartesianBraiding, 1 ],
          [ PreCompose, 1 ],
          [ ClassifyingMorphismOfSubobject, 1 ],
          [ DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential, 1 ] ],
        
  function( C, a, b, mu, Pa )
    local mu_, chi_;
    
    ## Î¼â»: s(Î¼) â†ª b Ã— a is the composition s(Î¼) â†ª a Ã— b â­‡ b Ã— a
    ## of Î¼: s(Î¼) â†ª a Ã— b and the canonical braiding a Ã— b â­‡ b Ã— a
    mu_ := PreCompose( C,
                   mu,
                   CartesianBraiding( C, a, b ) );
    
    ## Ï‡â»: b Ã— a â†’ Î© is the classifying morphism of Î¼â»: s(Î¼) â†ª b Ã— a
    chi_ := ClassifyingMorphismOfSubobject( C, mu_ );
    
    ## b â†’ P(a) encoding the relation given by Î¼
    return DirectProductToExponentialLeftAdjunctMorphismWithGivenExponential( C, b, a, chi_, Pa );
    
end );

## the currying {}: a â†ª Î©áµƒ of the classifying morphism of the diagonal relation Î” âŠ† a Ã— a
AddDerivationToCAP( SingletonMorphismWithGivenPowerObject,
        "",
        [ [ CartesianDiagonal, 1 ],
          [ UpperSegmentOfRelationWithGivenRange, 1 ] ],
        
  function( cat, a, Pa )
    local Delta, singleton_morphism;
    
    ## Î”: a â†’ a Ã— a
    Delta := CartesianDiagonal( cat, a, 2 );
    
    ## {}: a â†ª Î©áµƒ
    singleton_morphism := UpperSegmentOfRelationWithGivenRange( cat, a, a, Delta, Pa );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    SetIsMonomorphism( singleton_morphism, true );
    
    return singleton_morphism;
    
end );

##
AddDerivationToCAP( IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ CartesianSquareOfSubobjectClassifier, 1 ],
          [ PowerObject, 1 ],
          [ IdentityMorphism, 2 ],
          [ ProjectionInFactorOfDirectProductWithGivenDirectProduct, 2 ],
          [ ExponentialOnMorphismsWithGivenExponentials, 2 ],
          [ UniversalMorphismIntoDirectProductWithGivenDirectProduct, 1 ] ],
        
  function( cat, Exp_a_Omega2, a, PaxPa )
    local Omega, diagram, Omega2, Pa, tau, u;
    
    Omega := SubobjectClassifier( cat );
    
    diagram := [ Omega, Omega ];
    
    Omega2 := CartesianSquareOfSubobjectClassifier( cat );
    
    Pa := PowerObject( cat, a );
    
    ## [ Exp(a, Ï€â‚): Exp(a, Î©Â²) â†  Exp(a, Î©), Exp(a, Ï€â‚‚): Exp(a, Î©Â²) â†  Exp(a, Î©) ]
    tau := [ ExponentialOnMorphismsWithGivenExponentials( cat,
                   Exp_a_Omega2,
                   IdentityMorphism( cat, a ),
                   ProjectionInFactorOfDirectProductWithGivenDirectProduct( cat,
                           diagram,
                           1,
                           Omega2 ),
                   Pa ),
             ExponentialOnMorphismsWithGivenExponentials( cat,
                   Exp_a_Omega2,
                     IdentityMorphism( cat, a ),
                   ProjectionInFactorOfDirectProductWithGivenDirectProduct( cat,
                           diagram,
                           2,
                           Omega2 ),
                   Pa ) ];
    
    ## Exp(a, Î©Â²) â­‡ Exp(a, Î©) Ã— Exp(a, Î©)
    u := UniversalMorphismIntoDirectProductWithGivenDirectProduct( cat,
                 [ Pa, Pa ],
                 Exp_a_Omega2,
                 tau,
                 PaxPa );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    if HasIsCartesianClosedCategory( cat ) and IsCartesianClosedCategory( cat ) then
        SetIsIsomorphism( u, true );
    fi;
    
    return u;
    
end );

## âŠ¤_a: ðŸ™ â†ª Pa
AddDerivationToCAP( RelativeTruthMorphismOfTrueWithGivenObjects,
        "",
        [ [ PreCompose, 2 ],
          [ UniversalMorphismIntoTerminalObjectWithGivenTerminalObject, 1 ],
          [ TruthMorphismOfTrue, 1 ],
          [ DirectProduct, 1 ],
          [ ProjectionInFactorOfDirectProductWithGivenDirectProduct, 1 ],
          [ PTransposeMorphismWithGivenRange, 1 ] ],
        
  function( cat, T, a, Pa )
    local true_a, T_a, Txa;
    
    ## true_a: a â†’ ðŸ™ â†’ Î©
    true_a := PreCompose( cat,
                      ## a â†’ ðŸ™
                      UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat,
                              a,
                              T ),
                      ## ðŸ™ â†’ Î©
                      TruthMorphismOfTrue( cat ) );
    
    T_a := [ T, a ];
    
    ## ðŸ™ Ã— a
    Txa := DirectProduct( cat, T_a );
    
    ## PTranspose( ðŸ™ Ã— a â†’ a â†’ ðŸ™ â†’ Î© ) = ðŸ™ â†ª Pa
    return PTransposeMorphismWithGivenRange( cat,
                   T,
                   a,
                   PreCompose( cat,
                           ProjectionInFactorOfDirectProductWithGivenDirectProduct( cat,
                                   T_a,
                                   2,
                                   Txa ),
                   true_a ),
                   Pa );
    
end );

## âŠ¥_a: ðŸ™ â†ª Pa
AddDerivationToCAP( RelativeTruthMorphismOfFalseWithGivenObjects,
        "",
        [ [ PreCompose, 2 ],
          [ UniversalMorphismIntoTerminalObjectWithGivenTerminalObject, 1 ],
          [ TruthMorphismOfFalse, 1 ],
          [ DirectProduct, 1 ],
          [ ProjectionInFactorOfDirectProductWithGivenDirectProduct, 1 ],
          [ PTransposeMorphismWithGivenRange, 1 ] ],
        
  function( cat, T, a, Pa )
    local false_a, T_a, Txa;
    
    ## false_a: a â†’ ðŸ™ â†’ Î©
    false_a := PreCompose( cat,
                      ## a â†’ ðŸ™
                      UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat,
                              a,
                              T ),
                      ## ðŸ™ â†’ Î©
                      TruthMorphismOfFalse( cat ) );
    
    T_a := [ T, a ];
    
    ## ðŸ™ Ã— a
    Txa := DirectProduct( cat, T_a );
    
    ## PTranspose( ðŸ™ Ã— a â†’ a â†’ ðŸ™ â†’ Î© ) = ðŸ™ â†ª Pa
    return PTransposeMorphismWithGivenRange( cat,
                   T,
                   a,
                   PreCompose( cat,
                           ProjectionInFactorOfDirectProductWithGivenDirectProduct( cat,
                                   T_a,
                                   2,
                                   Txa ),
                   false_a ),
                   Pa );
    
end );

## âŠ¤_a: ðŸ™ â†ª Pa
AddDerivationToCAP( RelativeTruthMorphismOfTrueWithGivenObjects,
        "",
        [ [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfTrue, 1 ] ],
        
  function( cat, T, a, Pa )
    
    return ExponentialOnMorphismsWithGivenExponentials( cat,
                   T,
                   IdentityMorphism( cat, a ),
                   TruthMorphismOfTrue( cat ),
                   Pa );
    
end );

## âŠ¥_a: ðŸ™ â†ª Pa
AddDerivationToCAP( RelativeTruthMorphismOfFalseWithGivenObjects,
        "",
        [ [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfFalse, 1 ] ],
        
  function( cat, T, a, Pa )
    
    return ExponentialOnMorphismsWithGivenExponentials( cat,
                   T,
                   IdentityMorphism( cat, a ),
                   TruthMorphismOfFalse( cat ),
                   Pa );
    
end );

## Â¬_a: Pa â­‡ Pa
AddDerivationToCAP( RelativeTruthMorphismOfNotWithGivenObjects,
        "",
        [ [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfNot, 1 ] ],
        
  function( cat, Pa, a, Pa1 )
    
    return ExponentialOnMorphismsWithGivenExponentials( cat,
                   Pa,
                   IdentityMorphism( cat, a ),
                   TruthMorphismOfNot( cat ),
                   Pa1 );
    
end );

## âˆ§_a: Pa Ã— Pa â†’ Pa
AddDerivationToCAP( RelativeTruthMorphismOfAndWithGivenObjects,
        "",
        [ [ CartesianSquareOfSubobjectClassifier, 1 ],
          [ ExponentialOnObjects, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfAnd, 1 ],
          [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects, 1 ],
          [ InverseForMorphisms, 1 ],
          [ PreCompose, 1 ] ],
        
  function( cat, PaxPa, a, Pa )
    local Omega2, Exp_a_Omega2, Exp_a_mor, u;
    
    Omega2 := CartesianSquareOfSubobjectClassifier( cat );
    
    Exp_a_Omega2 := ExponentialOnObjects( cat, a, Omega2 );
    
    ## Exp(a, âˆ§): Exp(a, Î©Â²) â†’ Exp(a, Î©)
    Exp_a_mor := ExponentialOnMorphismsWithGivenExponentials( cat,
                         Exp_a_Omega2,
                         IdentityMorphism( cat, a ),
                         TruthMorphismOfAnd( cat ),
                         Pa );
    
    ## Exp(a, Î©Â²) â­‡ Exp(a, Î©) Ã— Exp(a, Î©)
    u := IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects( cat,
                 Exp_a_Omega2,
                 a,
                 PaxPa );
    
    ## Exp(a, Î©) Ã— Exp(a, Î©) â†’ Exp(a, Î©)
    return PreCompose( cat,
                   InverseForMorphisms( cat, u ),
                   Exp_a_mor );
    
end );

## âˆ¨_a: Pa Ã— Pa â†’ Pa
AddDerivationToCAP( RelativeTruthMorphismOfOrWithGivenObjects,
        "",
        [ [ CartesianSquareOfSubobjectClassifier, 1 ],
          [ ExponentialOnObjects, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfOr, 1 ],
          [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects, 1 ],
          [ InverseForMorphisms, 1 ],
          [ PreCompose, 1 ] ],
        
  function( cat, PaxPa, a, Pa )
    local Omega2, Exp_a_Omega2, Exp_a_mor, u;
    
    Omega2 := CartesianSquareOfSubobjectClassifier( cat );
    
    Exp_a_Omega2 := ExponentialOnObjects( cat, a, Omega2 );
    
    ## Exp(a, âˆ¨): Exp(a, Î©Â²) â†’ Exp(a, Î©)
    Exp_a_mor := ExponentialOnMorphismsWithGivenExponentials( cat,
                         Exp_a_Omega2,
                         IdentityMorphism( cat, a ),
                         TruthMorphismOfOr( cat ),
                         Pa );
    
    ## Exp(a, Î©Â²) â­‡ Exp(a, Î©) Ã— Exp(a, Î©)
    u := IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects( cat,
                 Exp_a_Omega2,
                 a,
                 PaxPa );
    
    ## Exp(a, Î©) Ã— Exp(a, Î©) â†’ Exp(a, Î©)
    return PreCompose( cat,
                   InverseForMorphisms( cat, u ),
                   Exp_a_mor );
    
end );

##
AddDerivationToCAP( RelativeTruthMorphismOfImpliesWithGivenObjects,
        "",
        [ [ CartesianSquareOfSubobjectClassifier, 1 ],
          [ ExponentialOnObjects, 1 ],
          [ IdentityMorphism, 1 ],
          [ TruthMorphismOfImplies, 1 ],
          [ ExponentialOnMorphismsWithGivenExponentials, 1 ],
          [ IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects, 1 ],
          [ InverseForMorphisms, 1 ],
          [ PreCompose, 1 ] ],
        
  function( cat, PaxPa, a, Pa )
    local Omega2, Exp_a_Omega2, Exp_a_mor, u;
    
    Omega2 := CartesianSquareOfSubobjectClassifier( cat );
    
    Exp_a_Omega2 := ExponentialOnObjects( cat, a, Omega2 );
    
    ## Exp(a, â‡’): Exp(a, Î©Â²) â†’ Exp(a, Î©)
    Exp_a_mor := ExponentialOnMorphismsWithGivenExponentials( cat,
                         Exp_a_Omega2,
                         IdentityMorphism( cat, a ),
                         TruthMorphismOfImplies( cat ),
                         Pa );
    
    ## Exp(a, Î©Â²) â­‡ Exp(a, Î©) Ã— Exp(a, Î©)
    u := IsomorphismOntoCartesianSquareOfPowerObjectWithGivenObjects( cat,
                 Exp_a_Omega2,
                 a,
                 PaxPa );
    
    ## Exp(a, Î©) Ã— Exp(a, Î©) â†’ Exp(a, Î©)
    return PreCompose( cat,
                   InverseForMorphisms( cat, u ),
                   Exp_a_mor );
    
end );

## Note that |Sub(Î©)| = |End(Î©)|
## * but id_Î© â‰œ âŠ¤_Î© âˆˆ Sub(Î©) does not correspond to id_Î© âˆˆ End(Î©) but to âŠ¤_Î©: Î© â†’ ðŸ™ â†’ Î© âˆˆ End(Î©), which is generally not an iso
## * and id_Î© âˆˆ End(Î©) corresponds to âŠ¤: ðŸ™ â†’ Î©

## -Î¹ is an operation Sub(X) â†’ Sub(X) induced by Â¬: Î© â†’ Î©
## Thm: For Î¹ âˆˆ Sub(Î©): -Î¹ = ( Î¹ â‡’ âŠ¥_Sub(Î©) )
## Cor: For Î¹ âˆˆ Sub(X): -Î¹ = ( Î¹ â‡’ âŠ¥_Sub(X) )
AddDerivationToCAP( EmbeddingOfPseudoComplementSubobject,
        "",
        [ [ SubobjectOfClassifyingMorphism, 1 ],
          [ PreCompose, 1 ],
          [ ClassifyingMorphismOfSubobject, 1 ],
          [ TruthMorphismOfNot, 1 ] ],
        
  function( cat, iota )
    # Î¹: S â†ª X
    
    return SubobjectOfClassifyingMorphism( ## -Î¹: (S - X) â†ª X
                   cat,
                   PreCompose( cat,
                           ClassifyingMorphismOfSubobject( cat, iota ), ## Ï‡_Î¹: X â†’ Î©
                           TruthMorphismOfNot( cat ) ) ); ## Â¬: Î© â†’ Î©
    
end );

##
AddDerivationToCAP( PseudoComplementSubobject,
        "",
        [ [ EmbeddingOfPseudoComplementSubobject, 1 ] ],
        
  function( cat, iota )
    
    return Source( EmbeddingOfPseudoComplementSubobject( cat, iota ) );
    
end );

## Î¹1 âˆ§ Î¹2 is an operation Sub(X) Ã— Sub(X) â†’ Sub(X) induced by âˆ§: Î© Ã— Î© â†’ Î©,
## however, we instead use the finite completeness and finite cocompletenss of the topos (see next method)
AddDerivationToCAP( EmbeddingOfIntersectionSubobject,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ SubobjectOfClassifyingMorphism, 1 ],
          [ PreCompose, 1 ],
          [ UniversalMorphismIntoDirectProduct, 1 ],
          [ ClassifyingMorphismOfSubobject, 2 ],
          [ TruthMorphismOfAnd, 1 ] ],
        
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( ## Î¹1 âˆ§ Î¹2
                   cat,
                   PreCompose( cat,
                           UniversalMorphismIntoDirectProduct( ## X = Target( Î¹1 ) = Target( Î¹2 ) â†’ Î© Ã— Î©
                                   cat,
                                   [ Omega, Omega ],
                                   Target( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfAnd( cat ) ) ); ## âˆ§: Î© Ã— Î© â†’ Î©
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Theorem 7.1.2]
AddDerivationToCAP( EmbeddingOfIntersectionSubobject,
        "",
        [ [ MorphismFromFiberProductToSink, 1 ],
          [ IsMonomorphism, 1 ] ],
        
  function( cat, iota1, iota2 )
    local subobject;
    
    subobject := MorphismFromFiberProductToSink( cat, [ iota1, iota2 ] );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    Assert( 4, IsMonomorphism( subobject ) );
    #% CAP_JIT_DROP_NEXT_STATEMENT
    SetIsMonomorphism( subobject, true );
    
    return subobject;
    
end );

##
AddDerivationToCAP( IntersectionSubobject,
        "",
        [ [ EmbeddingOfIntersectionSubobject, 1 ] ],
        
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfIntersectionSubobject( cat, iota1, iota2 ) );
    
end );

## Î¹1 âˆ¨ Î¹2 is an operation Sub(X) Ã— Sub(X) â†’ Sub(X) induced by âˆ¨: Î© Ã— Î© â†’ Î©
## however, we instead use the finite completeness and finite cocompletenss of the topos (see next method)
AddDerivationToCAP( EmbeddingOfUnionSubobject,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ SubobjectOfClassifyingMorphism, 1 ],
          [ PreCompose, 1 ],
          [ UniversalMorphismIntoDirectProduct, 1 ],
          [ ClassifyingMorphismOfSubobject, 2 ],
          [ TruthMorphismOfOr, 1 ] ],
        
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( cat, ## Î¹1 âˆ¨ Î¹2
                   PreCompose( cat,
                           UniversalMorphismIntoDirectProduct( ## X = Target( Î¹1 ) = Target( Î¹2 ) â†’ Î© Ã— Î©
                                   cat,
                                   [ Omega, Omega ],
                                   Target( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfOr( cat ) ) ); ## âˆ¨: Î© Ã— Î© â†’ Î©
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Theorem 7.1.3]
AddDerivationToCAP( EmbeddingOfUnionSubobject,
        "",
        [ [ ImageEmbedding, 1 ],
          [ UniversalMorphismFromCoproduct, 1 ] ],
        
  function( cat, iota1, iota2 )
    
    return ImageEmbedding( cat,
                   UniversalMorphismFromCoproduct( cat,
                           [ Source( iota1 ), Source( iota2 ) ],
                           Target( iota1 ),
                           [ iota1, iota2 ] ) );  ## [ Î¹1, Î¹2 ] : Source( Î¹1 ) âŠ” Source( Î¹2 ) â†’ Target( Î¹1 )
    
end );

##
AddDerivationToCAP( UnionSubobject,
        "",
        [ [ EmbeddingOfUnionSubobject, 1 ] ],
        
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfUnionSubobject( cat, iota1, iota2 ) );
    
end );

## Î¹1 â‡’ Î¹2 is an operation Sub(X) Ã— Sub(X) â†’ Sub(X) induced by â‡’: Î© Ã— Î© â†’ Î©
AddDerivationToCAP( EmbeddingOfRelativePseudoComplementSubobject,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ SubobjectOfClassifyingMorphism, 1 ],
          [ PreCompose, 1 ],
          [ UniversalMorphismIntoDirectProduct, 1 ],
          [ ClassifyingMorphismOfSubobject, 2 ],
          [ TruthMorphismOfImplies, 1 ] ],
        
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( cat, ## Î¹1 â‡’ Î¹2
                   PreCompose( cat,
                           UniversalMorphismIntoDirectProduct( cat, ## X = Target( Î¹1 ) = Target( Î¹2 ) â†’ Î© Ã— Î©
                                   [ Omega, Omega ],
                                   Target( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfImplies( cat ) ) ); ## â‡’: Î© Ã— Î© â†’ Î©
    
end );

##
AddDerivationToCAP( RelativePseudoComplementSubobject,
        "",
        [ [ EmbeddingOfRelativePseudoComplementSubobject, 1 ] ],
        
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfRelativePseudoComplementSubobject( cat, iota1, iota2 ) );
    
end );

##
AddDerivationToCAP( FiberMorphismWithGivenObjects,
        "FiberMorphismWithGivenObjects using PowerObjectEvaluationMorphism and PTransposeMorphism",
        [ [ PowerObject, 1 ],
          [ DirectProduct, 3 ],
          [ SubobjectClassifier, 1 ],
          [ PowerObjectEvaluationMorphismWithGivenObjects, 1 ],
          [ CartesianAssociatorRightToLeftWithGivenDirectProducts, 1 ],
          [ PreCompose, 1 ],
          [ PTransposeMorphismWithGivenRange, 1 ] ],
        
  function( cat, PBxC_xB, B, C, PC )
    local B_C, BxC, PBxC, PBxC_BxC, PBxC_xBxC, Omega, epsilon_BxC, PBxC_xB_xC, alpha, epsilon_BxC_;
    
    B_C := [ B, C ];
    
    ## B Ã— C
    BxC := DirectProduct( cat, B_C );
    
    ## P(B Ã— C)
    PBxC := PowerObject( cat, BxC );
    
    PBxC_BxC := [ PBxC, BxC ];
    
    ## P(B Ã— C) Ã— (B Ã— C)
    PBxC_xBxC := DirectProduct( cat, PBxC_BxC );
    
    ## Î©
    Omega := SubobjectClassifier( cat );
    
    ## Ïµ_{B Ã— C} : P(B Ã— C) Ã— (B Ã— C) â†’ Î©
    epsilon_BxC := PowerObjectEvaluationMorphismWithGivenObjects( cat,
                           PBxC_xBxC,
                           BxC,
                           Omega );
    
    ## (P(B Ã— C) Ã— B) Ã— C
    PBxC_xB_xC := DirectProduct( cat,
                          [ PBxC_xB, C ] );
    
    ## P(B Ã— C) Ã— (B Ã— C) â†’ (P(B Ã— C) Ã— B) Ã— C
    alpha := CartesianAssociatorRightToLeftWithGivenDirectProducts( cat,
                     PBxC_xBxC,
                     PBxC,
                     B,
                     C,
                     PBxC_xB_xC );
    
    ## Ïµ_{B Ã— C} : (P(B Ã— C) Ã— B) Ã— C â†’ Î©
    epsilon_BxC_ := PreCompose( cat,
                            alpha,
                            epsilon_BxC );
    
    ## v: P(B Ã— C) Ã— B â†’ PC, where
    ## v(R, b) = Ï€_Bâ»Â¹(b) âˆ© R = { c âˆˆ C | (b,c) âˆˆ R } âˆˆ PC
    return PTransposeMorphismWithGivenRange( cat,
                   PBxC_xB,
                   C,
                   epsilon_BxC_,
                   PC );
    
end );

##
CAP_INTERNAL_ADD_REPLACEMENTS_FOR_METHOD_RECORD(
        rec( SingletonSupportOfRelationsWithGivenObjects :=
             [ [ "PowerObject", 1 ],
               [ "DirectProduct", 2 ],
               [ "FiberMorphismWithGivenObjects", 1 ],
               [ "SingletonMorphismWithGivenPowerObject", 1 ],
               [ "ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier", 1 ],
               [ "PreCompose", 1 ],
               [ "PTransposeMorphismWithGivenRange", 1 ] ] ) );

##
InstallOtherMethodForCompilerForCAP( SingletonSupportOfRelationsWithGivenObjects,
        "for a category and four category objects",
        [ IsCapCategory, IsCapCategoryObject, IsCapCategoryObject, IsCapCategoryObject, IsCapCategoryObject ],
        
  function( cat, PBxC, B, C, PB )
    local PC, BxC, PBxC_xB, v, sing, sigma, v_sigma;
    
    PC := PowerObject( cat, C );
    
    ## B Ã— C
    BxC := DirectProduct( cat, [ B, C ] );
    
    ## P(B Ã— C) Ã— B
    PBxC_xB := DirectProduct( cat,
                       [ PBxC, B ] );
    
    ## v: P(B Ã— C) Ã— B â†’ PC, where
    ## v(R, b) = Ï€_Bâ»Â¹(b) âˆ© R = { c âˆˆ C | (b,c) âˆˆ R } âˆˆ PC
    v := FiberMorphismWithGivenObjects( cat,
                 PBxC_xB,
                 B,
                 C,
                 PC );
    
    ## {}_C: C â†ª PC
    sing := SingletonMorphismWithGivenPowerObject( cat,
                    C,
                    PC );
    
    ## Ïƒ_C: PC â†’ Î©
    sigma := ClassifyingMorphismOfSubobject( cat,
                     sing );
    
    ## v Ïƒ_C: P(B Ã— C) Ã— B â†’ Î©
    v_sigma := PreCompose( cat,
                       v,
                       sigma );
    
    ## u: P(B Ã— C) â†’ PB, where
    ## u(R) = { b âˆˆ B | v(R, b) is a singleton } âˆˆ PB,
    ## i.e., u(R) is the set of base points b, over which R is a singleton
    return PTransposeMorphismWithGivenRange( cat,
                   PBxC,
                   B,
                   v_sigma,
                   PB );
    
end );

## [MacLane-Moerdijk, p.168]
AddDerivationToCAP( ExponentialOnObjects,
        "ExponentialOnObjects from the power object, the power object evaluation morphism, and the P-transpose",
        [ [ PowerObject, 3 ],
          [ DirectProduct, 3 ],
          [ TerminalObject, 1 ],
          [ FiberMorphismWithGivenObjects, 1 ],
          [ SingletonMorphismWithGivenPowerObject, 1 ],
          [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ PreCompose, 1 ],
          [ PTransposeMorphismWithGivenRange, 1 ],
          [ RelativeTruthMorphismOfTrueWithGivenObjects, 1 ],
          [ FiberProduct, 1 ] ],
        
  function( cat, B, C )
    local PB, BxC, PBxC, u, true_B;
    
    PB := PowerObject( cat, B );
    
    ## B Ã— C
    BxC := DirectProduct( cat, [ B, C ] );
    
    ## P(B Ã— C)
    PBxC := PowerObject( cat, BxC );
    
    ## u: P(B Ã— C) â†’ PB, where
    ## u(R) = { b âˆˆ B | v(R, b) is a singleton } âˆˆ PB,
    ## i.e., u(R) is the set of base points b, over which R is a singleton
    u := SingletonSupportOfRelationsWithGivenObjects( cat,
                 PBxC,
                 B, C,
                 PB );
    
    ## ðŸ™ â†ª PB, * â†¦ B
    true_B := RelativeTruthMorphismOfTrueWithGivenObjects( cat,
                      TerminalObject( cat ),
                      B,
                      PB );
    
    ## the set of all relations that are graphs of functions
    return FiberProduct( cat,
                   [ u, true_B ] );
    
end );

##
AddDerivationToCAP( CartesianLeftEvaluationMorphismWithGivenSource,
        "CartesianLeftEvaluationMorphismWithGivenSource from the power object, the power object evaluation morphism, and the P-transpose",
        [ [ PowerObject, 4 ],
          [ DirectProduct, 4 ],
          [ FiberMorphismWithGivenObjects, 2 ],
          [ SingletonMorphismWithGivenPowerObject, 2 ],
          [ ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier, 1 ],
          [ PreCompose, 2 ],
          [ PTransposeMorphismWithGivenRange, 1 ],
          [ TerminalObject, 1 ],
          [ RelativeTruthMorphismOfTrueWithGivenObjects, 1 ],
          [ ProjectionInFactorOfFiberProduct, 1 ],
          [ DirectProductFunctorialWithGivenDirectProducts, 1 ],
          [ IdentityMorphism, 1 ],
          [ LiftAlongMonomorphism, 1 ] ],
        
  function( cat, B, C, CB_xB ) ## CB_xB = Cá´® Ã— B
    local PB, PC, BxC, PBxC, PBxC_B, PBxC_xB, v, sing, sigma, v_sigma, u, true_B, m, mx1;
    
    PB := PowerObject( B );
    
    PC := PowerObject( C );
    
    ## B Ã— C
    BxC := DirectProduct( cat, [ B, C ] );
    
    ## P(B Ã— C)
    PBxC := PowerObject( cat, BxC );
    
    PBxC_B := [ PBxC, B ];
    
    ## P(B Ã— C) Ã— B
    PBxC_xB := DirectProduct( cat,
                       PBxC_B );
    
    ## v: P(B Ã— C) Ã— B â†’ PC, where
    ## v(R, b) = Ï€_Bâ»Â¹(b) âˆ© R = { c âˆˆ C | (b,c) âˆˆ R } âˆˆ PC
    v := FiberMorphismWithGivenObjects( cat,
                 PBxC_xB,
                 B, C,
                 PC );
    
    ## {}_C: C â†ª PC
    sing := SingletonMorphismWithGivenPowerObject( cat,
                    C,
                    PC );
    
    ## u: P(B Ã— C) â†’ PB, where
    ## u(R) = { b âˆˆ B | v(R, b) is a singleton } âˆˆ PB,
    ## i.e., u(R) is the set of base points b, over which R is a singleton
    u := SingletonSupportOfRelationsWithGivenObjects( cat,
                 PBxC,
                 B, C,
                 PB );
    
    ## ðŸ™ â†ª PB, * â†¦ B
    true_B := RelativeTruthMorphismOfTrueWithGivenObjects( cat,
                      TerminalObject( cat ),
                      B,
                      PB );
    
    ## m: Cá´® â†’ P(B Ã— C)
    m := ProjectionInFactorOfFiberProduct( cat,
                 [ u, true_B ],
                 1 );
    
    ## m Ã— 1 : Cá´® Ã— B â†’ P(C Ã— B) Ã— B
    mx1 := DirectProductFunctorialWithGivenDirectProducts( cat,
                   CB_xB,
                   [ Source( m ), B ],
                   [ m, IdentityMorphism( cat, B ) ],
                   PBxC_B,
                   PBxC_xB );
    
    ## Cá´® Ã— B â†’ C
    return LiftAlongMonomorphism( cat,
                   ## {}: C â†ª PC
                   sing,
                   ## Cá´® Ã— B â†’ PC
                   PreCompose( cat,
                           ## Cá´® Ã— B â†’ P(C Ã— B) Ã— B
                           mx1,
                           ## P(C Ã— B) Ã— B â†’ PC
                           v ) );
    
end );

##
AddDerivationToCAP( MorphismsOfExternalHom,
        "MorphismsOfExternalHom using MorphismsOfExternalHom in RangeCategoryOfHomomorphismStructure",
        [ [ HomomorphismStructureOnObjects, 1 ],
          [ DistinguishedObjectOfHomomorphismStructure, 1 ],
          [ InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism, 2 ],
          [ MorphismsOfExternalHom, 1, RangeCategoryOfHomomorphismStructure ] ],
        
  function( cat, A, B )
    local range_cat, hom_A_B, D, morphisms;
    
    range_cat := RangeCategoryOfHomomorphismStructure( cat );
    
    hom_A_B := HomomorphismStructureOnObjects( cat, A, B );
    
    D := DistinguishedObjectOfHomomorphismStructure( cat );
    
    morphisms := MorphismsOfExternalHom( range_cat,
                         D, hom_A_B );
    
    return List( morphisms,
                 phi -> InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism( cat,
                         A,
                         B,
                         phi ) );
    
end :
  CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
  CategoryFilter := HasRangeCategoryOfHomomorphismStructure );

##
AddDerivationToCAP( ListOfSubobjects,
        "",
        [ [ SubobjectClassifier, 1 ],
          [ MorphismsOfExternalHom, 1 ],
          [ SubobjectOfClassifyingMorphism, 2 ] ],
        
  function( cat, A )
    local Omega, chis;
    
    Omega := SubobjectClassifier( cat );
    
    chis := MorphismsOfExternalHom( cat, A, Omega );
    
    return List( chis,
                 chi -> SubobjectOfClassifyingMorphism( cat, chi ) );
    
end );

##
AddDerivationToCAP( LawvereTierneyLocalModalityOperators,
        "",
        [ [ DirectProductFunctorial, 2 ],
          [ IsEqualForMorphisms, 6 ],
          [ MorphismsOfExternalHom, 1 ],
          [ PreCompose, 8 ],
          [ SubobjectClassifier, 1 ],
          [ TruthMorphismOfAnd, 1 ],
          [ TruthMorphismOfTrue, 1 ] ],
        
  function( cat )
    local Omega, endos, idemp, t, jtrue, a;
    
    Omega := SubobjectClassifier( cat );
    
    endos := MorphismsOfExternalHom( cat, Omega, Omega );
    
    idemp := Filtered( endos, j -> IsEqualForMorphisms( cat, j, PreCompose( cat, j, j ) ) ); # j^2 = j
    
    ## âŠ¤: ðŸ™ â†ª Î©
    t := TruthMorphismOfTrue( cat );
    
    jtrue := Filtered( idemp, j -> IsEqualForMorphisms( cat, t, PreCompose( cat, t, j ) ) ); # true â‹… j = j
    
    ## âˆ§: Î© Ã— Î© â†’ Î©
    a := TruthMorphismOfAnd( cat );
    
    return Filtered( jtrue, j ->
                   IsEqualForMorphisms( cat,
                           PreCompose( cat, a, j ), # âˆ§ â‹… j
                           PreCompose( cat, # ( j Ã— j ) â‹… âˆ§
                                   DirectProductFunctorial( cat, # j Ã— j
                                           [ Omega, Omega ],
                                           [ j, j ],
                                           [ Omega, Omega ] ),
                                   a ) ) );
    
end );

##
AddDerivationToCAP( LawvereTierneySubobjects,
        "",
        [ [ LawvereTierneyLocalModalityOperators, 1 ],
          [ SubobjectOfClassifyingMorphism, 2 ] ],
        
  function( cat )
    local LT;
    
    LT := LawvereTierneyLocalModalityOperators( cat );
    
    return List( LT, j -> SubobjectOfClassifyingMorphism( cat, j ) );
    
end );

##
AddDerivationToCAP( LawvereTierneyEmbeddingsOfSubobjectClassifiers,
        "",
        [ [ LawvereTierneyLocalModalityOperators, 1 ],
          [ ImageEmbedding, 2 ] ],
        
  function( cat )
    local LT;
    
    LT := LawvereTierneyLocalModalityOperators( cat );
    
    return List( LT, j -> ImageEmbedding( cat, j ) );
    
end );

##
AddDerivationToCAP( HomomorphismStructureOnMorphismsWithGivenObjects,
        "",
        [ [ DistinguishedObjectOfHomomorphismStructure, 1 ],
          [ ExactCoverWithGlobalElements, 1, RangeCategoryOfHomomorphismStructure ],
          [ InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism, 2 ],
          [ PreComposeList, 2 ],
          [ InterpretMorphismAsMorphismFromDistinguishedObjectToHomomorphismStructureWithGivenObjects, 2 ],
          [ UniversalMorphismFromCoproductWithGivenCoproduct, 1, RangeCategoryOfHomomorphismStructure ] ],
        
  function( cat, source, alpha, gamma, range )
    local range_cat, distinguished_object, Ls, source_alpha, range_gamma, tau;
    
    range_cat := RangeCategoryOfHomomorphismStructure( cat );
    distinguished_object := DistinguishedObjectOfHomomorphismStructure( cat );
    
    Ls := ExactCoverWithGlobalElements( range_cat, source );
    
    source_alpha := Source( alpha );
    range_gamma := Target( gamma );
    
    tau := List( Ls, mor ->
                 InterpretMorphismAsMorphismFromDistinguishedObjectToHomomorphismStructureWithGivenObjects( cat,
                         distinguished_object,
                         PreComposeList( cat,
                                 source_alpha,
                                 [ alpha,
                                   InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism( cat,
                                           Target( alpha ),
                                           Source( gamma ),
                                           mor ),
                                   gamma ],
                                 range_gamma ),
                         range ) );
    
    return UniversalMorphismFromCoproductWithGivenCoproduct( range_cat,
                   List( tau, Source ),
                   range,
                   tau,
                   source );
    
end :
  CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
  CategoryFilter := cat -> HasRangeCategoryOfHomomorphismStructure( cat ) );

##
AddDerivationToCAP( CoimageProjectionWithGivenCoimageObject,
        "CoimageProjection as the coastriction to image",
        [ [ ImageObject, 1 ],
          [ CoastrictionToImageWithGivenImageObject, 1 ],
          [ InverseOfMorphismFromCoimageToImageWithGivenObjects, 1 ],
          [ PreCompose, 1 ] ],
        
  function( cat, mor, coimage )
    local image, coast, iso;
    
    image := ImageObject( cat, mor );
    
    coast := CoastrictionToImageWithGivenImageObject( cat, mor, image );
    
    iso := InverseOfMorphismFromCoimageToImageWithGivenObjects( cat, image, mor, coimage );
    
    return PreCompose( cat, coast, iso );
    
end );

##
AddDerivationToCAP( MorphismFromCoimageToImageWithGivenObjects,
                    "MorphismFromCoimageToImageWithGivenObjects using that the image embedding lifts the coimage astriction",
                    [ [ ImageEmbeddingWithGivenImageObject, 1 ],
                      [ AstrictionToCoimageWithGivenCoimageObject, 1 ],
                      [ LiftAlongMonomorphism, 1 ] ],
                    
  function( cat, coimage, morphism, image )
    
    return LiftAlongMonomorphism( cat,
                   ImageEmbeddingWithGivenImageObject( cat, morphism, image ),
                   AstrictionToCoimageWithGivenCoimageObject( cat, morphism, coimage ) );
    
end : CategoryFilter := IsElementaryTopos );

##
AddDerivationToCAP( InverseOfMorphismFromCoimageToImageWithGivenObjects,
                    "InverseOfMorphismFromCoimageToImageWithGivenObjects as the inverse of MorphismFromCoimageToImage",
                    [ [ InverseForMorphisms, 1 ],
                      [ MorphismFromCoimageToImageWithGivenObjects, 1 ] ],
                    
  function( cat, image, morphism, coimage )
    
    return InverseForMorphisms( cat, MorphismFromCoimageToImageWithGivenObjects( cat, coimage, morphism, image ) );
    
end : CategoryFilter := IsElementaryTopos );

## Final derivations

##
AddFinalDerivationBundle( "MorphismFromCoimageToImageWithGivenObjects as the identity on the image object",
        [ [ ImageObject, 1 ],
          [ IdentityMorphism, 1 ] ],
        [ CoimageObject,
          MorphismFromCoimageToImageWithGivenObjects,
          InverseOfMorphismFromCoimageToImageWithGivenObjects,
          CoimageObject,
          CoimageProjection,
          CoimageProjectionWithGivenCoimageObject,
          AstrictionToCoimage,
          AstrictionToCoimageWithGivenCoimageObject,
          UniversalMorphismIntoCoimage,
          UniversalMorphismIntoCoimageWithGivenCoimageObject,
          IsomorphismFromCoimageToCokernelOfKernel,
          IsomorphismFromCokernelOfKernelToCoimage ],
        
[
  CoimageObject,
  [ [ ImageObject, 1 ] ],
  function( cat, mor )
    
    return ImageObject( cat, mor );
    
  end
],
[
  MorphismFromCoimageToImageWithGivenObjects,
  [ [ IdentityMorphism, 1 ] ],
  function( cat, coimage, mor, image )
    
    return IdentityMorphism( cat, image );
    
  end
],
[
  InverseOfMorphismFromCoimageToImageWithGivenObjects,
  [ [ IdentityMorphism, 1 ] ],
  function( cat, image, mor, coimage )
    
    return IdentityMorphism( cat, image );
    
  end
] : CategoryFilter := IsElementaryTopos );

##
AddFinalDerivationBundle( "adding the homomorphism structure using MorphismsOfExternalHom",
        [ [ TerminalObject, 1, RangeCategoryOfHomomorphismStructure ],
          [ MorphismsOfExternalHom, 2 ],
          [ MorphismsOfExternalHom, 1, RangeCategoryOfHomomorphismStructure ],
          [ ObjectConstructor, 1, RangeCategoryOfHomomorphismStructure ],
          [ PreComposeList, 2 ],
          [ MorphismConstructor, 1, RangeCategoryOfHomomorphismStructure ],
          ],
        [ DistinguishedObjectOfHomomorphismStructure,
          HomomorphismStructureOnObjects,
          HomomorphismStructureOnMorphisms,
          HomomorphismStructureOnMorphismsWithGivenObjects,
          InterpretMorphismAsMorphismFromDistinguishedObjectToHomomorphismStructure,
          InterpretMorphismAsMorphismFromDistinguishedObjectToHomomorphismStructureWithGivenObjects,
          InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism
          ],
        
[
  DistinguishedObjectOfHomomorphismStructure,
  [ [ TerminalObject, 1, RangeCategoryOfHomomorphismStructure ] ],
  function( cat )
    local H;
    
    H := RangeCategoryOfHomomorphismStructure( cat );
    
    return TerminalObject( H );
    
  end
],
[
  HomomorphismStructureOnObjects,
  [ [ ObjectConstructor, 1, RangeCategoryOfHomomorphismStructure ],
    [ MorphismsOfExternalHom, 1 ] ],
  function( cat, a, b )
    local H;
    
    H := RangeCategoryOfHomomorphismStructure( cat );
    
    return ObjectConstructor( H,
                   Length( MorphismsOfExternalHom( cat, a, b ) ) );
    
  end
],
[
  HomomorphismStructureOnMorphismsWithGivenObjects,
  [ [ MorphismsOfExternalHom, 2 ],
    [ PreComposeList, 2 ],
    [ MorphismConstructor, 1, RangeCategoryOfHomomorphismStructure ] ],
  function( cat, s, alpha, gamma, r )
    local H, source_alpha, range_gamma, s_mors, r_mors, images;
    
    H := RangeCategoryOfHomomorphismStructure( cat );
    
    source_alpha := Source( alpha );
    range_gamma := Target( gamma );
    
    # r_mor = alpha s_mor gamma = Source( alpha ) --alpha-> Target( alpha ) --s_mor-> Source( gamma ) --gamma-> Target( gamma )
    
    s_mors := MorphismsOfExternalHom( cat, Target( alpha ), Source( gamma ) );
    r_mors := MorphismsOfExternalHom( cat, Source( alpha ), Target( gamma ) );
    
    images := List( s_mors, s_mor -> -1 + SafePosition( r_mors, PreComposeList( cat, source_alpha, [ alpha, s_mor, gamma ], range_gamma ) ) );
    
    return MorphismConstructor( H,
                   s,
                   images,
                   r );
    
  end
],
[
  InterpretMorphismAsMorphismFromDistinguishedObjectToHomomorphismStructureWithGivenObjects,
  [ [ MorphismsOfExternalHom, 1 ],
    [ MorphismConstructor, 1, RangeCategoryOfHomomorphismStructure ] ],
  function( cat, t, alpha, r )
    local H, mors;
    
    H := RangeCategoryOfHomomorphismStructure( cat );
    
    mors := MorphismsOfExternalHom( cat, Source( alpha ), Target( alpha ) );
    
    return MorphismConstructor( H,
                   t,
                   [ -1 + SafePosition( mors, alpha ) ],
                   r );
    
  end
],
[
  InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism,
  [ [ MorphismsOfExternalHom, 1 ],
    [ MorphismsOfExternalHom, 1, RangeCategoryOfHomomorphismStructure ] ],
  function( cat, a, b, iota )
    local H, mors_H, pos;
    
    H := RangeCategoryOfHomomorphismStructure( cat );
    
    # 1_H -> Hom( a, b )
    mors_H := MorphismsOfExternalHom( H, Source( iota ), Target( iota ) );
    
    pos := SafePosition( mors_H, iota );
    
    return MorphismsOfExternalHom( cat, a, b )[pos];
    
  end
] : CategoryGetters := rec( H := RangeCategoryOfHomomorphismStructure ),
    CategoryFilter := function( cat )
      return HasRangeCategoryOfHomomorphismStructure( cat ) and
             IsBoundGlobal( "IsSkeletalCategoryOfFiniteSets" ) and
             ValueGlobal( "IsSkeletalCategoryOfFiniteSets" )( RangeCategoryOfHomomorphismStructure( cat ) );
    end
);
