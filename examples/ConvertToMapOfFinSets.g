#! @Chunk ConvertToMapOfFinSets

LoadPackage( "CatReps" );

#! @Example
c3c3 := ConcreteCategory([[2,3,1],[4,5,6],[,,,5,6,4]]);
#! rec( codomain := [ 1, 2, 2 ], domain := [ 1, 1, 2 ],
#!      generators := [ [ 2, 3, 1 ], [ 4, 5, 6 ], [ ,,, 5, 6, 4 ] ],
#!      objects := [ [ 1, 2, 3 ], [ 4, 5, 6 ] ], operations := rec(  ) )
objects := List( c3c3.objects, FinSet );
#! [ <An object in FinSets>, <An object in FinSets> ]
g1 := ConvertToMapOfFinSets( objects, c3c3.generators[1] );
#! <A morphism in FinSets>
Display( g1 );
#! [ [ 1, 2, 3 ], [ [ 1, 2 ], [ 2, 3 ], [ 3, 1 ] ], [ 1, 2, 3 ] ]
g2 := ConvertToMapOfFinSets( objects, c3c3.generators[2] );
#! <A morphism in FinSets>
Display( g2 );
#! [ [ 1, 2, 3 ], [ [ 1, 4 ], [ 2, 5 ], [ 3, 6 ] ], [ 4, 5, 6 ] ]
g3 := ConvertToMapOfFinSets( objects, c3c3.generators[3] );
#! <A morphism in FinSets>
Display( g3 );
#! [ [ 4, 5, 6 ], [ [ 4, 5 ], [ 5, 6 ], [ 6, 4 ] ], [ 4, 5, 6 ] ]
g := ConvertToMapOfFinSets( objects, [,,,1,1,1] );
#! <A morphism in FinSets>
Display( g );
#! [ [ 4, 5, 6 ], [ [ 4, 1 ], [ 5, 1 ], [ 6, 1 ] ], [ 1, 2, 3 ] ]
#! @EndExample

#! We can also create finite concrete categories with objects 
#! not starting with $1$, to demonstrate that
#! <C>ConcreteCategoryForCAP( [ [,,,5,6,4], [,,,7,8,9], [,,,,,,8,9,7] ] )</C>
#! and <C>ConcreteCategoryForCAP( [ [2,3,1], [4,5,6], [,,,5,6,4] ] )</C> yield
#! isomorphic categories, in particular, their underlying quivers are
#! isomorphic (here even equal), inducing isomorphic algebroids and
#! isomorphic categories of representations.

#! @Example
ccat := ConcreteCategoryForCAP( [ [2,3,1], [4,5,6], [,,,5,6,4] ] );
#! A finite concrete category
ccat2 := ConcreteCategoryForCAP( [ [,,,5,6,4], [,,,7,8,9], [,,,,,,8,9,7] ] );
#! A finite concrete category
GF3 := HomalgRingOfIntegers(3);
#! GF(3)
A1 := Algebroid( GF3, ccat );
#! Algebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
A2 := Algebroid( GF3, ccat2 );
#! Algebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
A1 = A2;
#! true
CatReps1 := Hom( A1, GF3 );
#! The category of functors: Algebroid generated by the right quiver 
#! q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over GF(3)
CatReps2 := Hom( A2, GF3 );
#! The category of functors: Algebroid generated by the right quiver 
#! q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over GF(3)
CatReps1 = CatReps2;
#! true
#! @EndExample
