# SPDX-License-Identifier: GPL-2.0-or-later
# Toposes: Elementary toposes
#
# Implementations
#

##
AddDerivationToCAP( SubobjectOfClassifyingMorphism,
  function( cat, mor )
    local truth, subobject;
    
    truth := TruthMorphismOfTrueWithGivenObjects(
                     cat,
                     TerminalObject( cat ),
                     SubobjectClassifier( cat ) );
    
    subobject := ProjectionInFactorOfFiberProduct( cat, [ mor, truth ], 1 );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    Assert( 4, IsMonomorphism( subobject ) );
    #% CAP_JIT_DROP_NEXT_STATEMENT
    SetIsMonomorphism( subobject, true );
    
    return subobject;
    
end : Description := "SubobjectOfClassifyingMorphism using the fiber product along the true morphism" );

##
AddDerivationToCAP( CartesianSquareOfSubobjectClassifier,
  function( cat )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return DirectProduct( cat, [ Omega, Omega ] );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Exercise 4.2.1]
## âŠ¤: ðŸ™ â†ª Î© classfies id_ðŸ™: ðŸ™ â†ª ðŸ™
AddDerivationToCAP( TruthMorphismOfTrueWithGivenObjects,
  function( cat, T, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   IdentityMorphism( cat, T ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 5.4 (False)]
## âŠ¥: ðŸ™ â†ª Î© classifies âˆ… â†ª ðŸ™
AddDerivationToCAP( TruthMorphismOfFalseWithGivenObjects,
  function( cat, T, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat, InitialObject( cat ), T ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## Â¬: Î© â†’ Î© classifies âŠ¥: ðŸ™ â†ª Î©
AddDerivationToCAP( TruthMorphismOfNotWithGivenObjects,
  function( cat, Omega, Omega1 )
    local T;
    
    T := TerminalObject( cat );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   TruthMorphismOfFalseWithGivenObjects( cat, T, Omega ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## âˆ§: Î© Ã— Î© â†’ Î© classifies the product morphism âŸ¨ âŠ¤, âŠ¤ âŸ©: ðŸ™ â†ª Î© Ã— Î© of twice the morphism âŠ¤: ðŸ™ â†ª Î©
AddDerivationToCAP( TruthMorphismOfAndWithGivenObjects,
  function( cat, Omega2, Omega )
    local T, t;
    
    T := TerminalObject( cat );
    
    t := TruthMorphismOfTrueWithGivenObjects( cat, T, Omega );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   UniversalMorphismIntoDirectProductWithGivenDirectProduct(
                           cat,
                           [ Omega, Omega ],
                           T,
                           [ t, t ],
                           Omega2 ),
                   Omega );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Section 6.6 (Truth-arrows in a topos)]
## âˆ¨: Î© Ã— Î© â†’ Î© classifies [ âŸ¨âŠ¤_Î©,id_Î©âŸ©, âŸ¨id_Î©,âŠ¤_Î©âŸ© ]: Î© âŠ” Î© â†ª Î© Ã— Î©
AddDerivationToCAP( TruthMorphismOfOrWithGivenObjects,
  function( cat, Omega2, Omega )
    local T, t, id, left, right;
    
    T := TerminalObject( cat );
    
    ## âŠ¤_Î©: Î© â†’ ðŸ™ â†’ Î© is the morphism classifying the full subobject of Î©, i.e.,
    ## âŠ¤_Î© = ClassifyingMorphismOfSubobject( IdentityMorphism( Omega ) )
    t := PreCompose(
                 cat,
                 UniversalMorphismIntoTerminalObjectWithGivenTerminalObject( cat, Omega, T ),
                 TruthMorphismOfTrueWithGivenObjects( cat, T, Omega ) );
    
    id := IdentityMorphism( cat, Omega );
    
    left := UniversalMorphismIntoDirectProductWithGivenDirectProduct(
                    cat,
                    [ Omega, Omega ],
                    Omega,
                    [ t, id ],
                    Omega2 );
    
    right := UniversalMorphismIntoDirectProductWithGivenDirectProduct(
                     cat,
                     [ Omega, Omega ],
                     Omega,
                     [ id, t ],
                     Omega2 );
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   UniversalMorphismFromCoproduct(
                           cat,
                           [ Omega, Omega ],
                           Omega2,
                           [ left, right ] ),
                   Omega );
    
end );

## â‡’: Î© Ã— Î© â†’ Î© classifies the equalizer embedding E â†ª Î© Ã— Î© of (âˆ§: Î© Ã— Î© â†’ Î©, Ï€_1: Î© Ã— Î© â†’ Î©)
AddDerivationToCAP( TruthMorphismOfImpliesWithGivenObjects,
  function( cat, Omega2, Omega )
    
    return ClassifyingMorphismOfSubobjectWithGivenSubobjectClassifier(
                   cat,
                   EmbeddingOfEqualizer(
                           cat,
                           Omega2,
                           [ TruthMorphismOfAndWithGivenObjects( cat, Omega2, Omega ),
                             ProjectionInFactorOfDirectProductWithGivenDirectProduct(
                                     cat,
                                     [ Omega, Omega ],
                                     1,
                                     Omega2
                                     ) ] ),
                   Omega );
    
end );

## Note that |Sub(Î©)| = |End(Î©)|
## * but id_Î© â‰œ âŠ¤_Î© âˆˆ Sub(Î©) does not correspond to id_Î© âˆˆ End(Î©) but to âŠ¤_Î©: Î© â†’ ðŸ™ â†’ Î© âˆˆ End(Î©), which is generally not an iso
## * and id_Î© âˆˆ End(Î©) corresponds to âŠ¤: ðŸ™ â†’ Î©

## -Î¹ is an operation Sub(Î©) â†’ Sub(Î©) induced by Â¬: Î© â†’ Î©
## Theorem: -Î¹ = ( Î¹ â‡’ âŠ¥_Sub(Î©) )
AddDerivationToCAP( EmbeddingOfPseudoComplementSubobject,
  function( cat, iota ) # Î¹: S â†ª M
    
    return SubobjectOfClassifyingMorphism( ## -Î¹: (S - M) â†ª M
                   cat,
                   PreCompose(
                           cat,
                           ClassifyingMorphismOfSubobject( cat, iota ), ## Ï‡_Î¹: M â†’ Î©
                           TruthMorphismOfNot( cat ) ## Â¬: Î© â†’ Î©
                           ) );
    
end );

##
AddDerivationToCAP( PseudoComplementSubobject,
  function( cat, iota )
    
    return Source( EmbeddingOfPseudoComplementSubobject( cat, iota ) );
    
end );

## Î¹1 âˆ§ Î¹2 is an operation Sub(Î©) Ã— Sub(Î©) â†’ Sub(Î©) induced by âˆ§: Î© Ã— Î© â†’ Î©,
## however, we instead use the finite completeness and finite cocompletenss of the topos (see next method)
AddDerivationToCAP( EmbeddingOfIntersectionSubobject,
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( ## -Î¹
                   cat,
                   PreCompose(
                           UniversalMorphismIntoDirectProduct( ## Range( Î¹1 ) = Range( Î¹2 ) â†’ Î© Ã— Î©
                                   cat,
                                   [ Omega, Omega ],
                                   Range( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfAnd( cat ) ## âˆ§: Î© Ã— Î© â†’ Î©
                           ) );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Theorem 7.1.2]
AddDerivationToCAP( EmbeddingOfIntersectionSubobject,
  function( cat, iota1, iota2 )
    local subobject;
    
    subobject := MorphismFromFiberProductToSink( cat, [ iota1, iota2 ] );
    
    #% CAP_JIT_DROP_NEXT_STATEMENT
    Assert( 4, IsMonomorphism( subobject ) );
    #% CAP_JIT_DROP_NEXT_STATEMENT
    SetIsMonomorphism( subobject, true );
    
    return subobject;
    
end );

##
AddDerivationToCAP( IntersectionSubobject,
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfIntersectionSubobject( cat, iota1, iota2 ) );
    
end );

## Î¹1 âˆ¨ Î¹2 is an operation Sub(Î©) Ã— Sub(Î©) â†’ Sub(Î©) induced by âˆ¨: Î© Ã— Î© â†’ Î©
## however, we instead use the finite completeness and finite cocompletenss of the topos (see next method)
AddDerivationToCAP( EmbeddingOfUnionSubobject,
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( ## -Î¹
                   cat,
                   PreCompose(
                           UniversalMorphismIntoDirectProduct( ## Range( Î¹1 ) = Range( Î¹2 ) â†’ Î© Ã— Î©
                                   cat,
                                   [ Omega, Omega ],
                                   Range( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfOr( cat ) ## âˆ¨: Î© Ã— Î© â†’ Î©
                           ) );
    
end );

## [Goldblatt 1984: Topoi - The categorical analysis of logic, Theorem 7.1.3]
AddDerivationToCAP( EmbeddingOfUnionSubobject,
  function( cat, iota1, iota2 )
    
    return ImageEmbedding(
                   cat,
                   UniversalMorphismFromCoproduct(
                           cat,
                           [ Source( iota1 ), Source( iota2 ) ],
                           Range( iota1 ),
                           [ iota1, iota2 ] ) );  ## [ Î¹1, Î¹2 ] : Source( Î¹1 ) âŠ” Source( Î¹2 ) â†’ Range( Î¹1 )
    
end );

##
AddDerivationToCAP( UnionSubobject,
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfUnionSubobject( cat, iota1, iota2 ) );
    
end );

## Î¹1 â‡’ Î¹2 is an operation Sub(Î©) Ã— Sub(Î©) â†’ Sub(Î©) induced by â‡’: Î© Ã— Î© â†’ Î©
AddDerivationToCAP( EmbeddingOfRelativePseudoComplementSubobject,
  function( cat, iota1, iota2 )
    local Omega;
    
    Omega := SubobjectClassifier( cat );
    
    return SubobjectOfClassifyingMorphism( ## -Î¹
                   cat,
                   PreCompose(
                           cat,
                           UniversalMorphismIntoDirectProduct( ## Range( Î¹1 ) = Range( Î¹2 ) â†’ Î© Ã— Î©
                                   cat,
                                   [ Omega, Omega ],
                                   Range( iota1 ),
                                   [ ClassifyingMorphismOfSubobject( cat, iota1 ), ## Ï‡_Î¹1
                                     ClassifyingMorphismOfSubobject( cat, iota2 ) ] ), ## Ï‡_Î¹2
                           TruthMorphismOfImplies( cat ) ## â‡’: Î© Ã— Î© â†’ Î©
                           ) );
    
end );

##
AddDerivationToCAP( RelativePseudoComplementSubobject,
  function( cat, iota1, iota2 )
    
    return Source( EmbeddingOfRelativePseudoComplementSubobject( cat, iota1, iota2 ) );
    
end );

##
AddDerivationToCAP( MorphismsOfExternalHom,
                    [ [ HomomorphismStructureOnObjects, 1 ],
                      [ DistinguishedObjectOfHomomorphismStructure, 1 ],
                      [ InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism, 2 ],
                      [ MorphismsOfExternalHom, 1, RangeCategoryOfHomomorphismStructure ],
                    ],
  function ( cat, A, B )
    local range_cat, hom_A_B, D, morphisms;
    
    range_cat := RangeCategoryOfHomomorphismStructure( cat );
    
    hom_A_B := HomomorphismStructureOnObjects( cat, A, B );
    
    D := DistinguishedObjectOfHomomorphismStructure( cat );
    
    morphisms := MorphismsOfExternalHom( range_cat,
                         D, hom_A_B );
    
    return List( morphisms,
                 phi -> InterpretMorphismFromDistinguishedObjectToHomomorphismStructureAsMorphism( cat,
                         A,
                         B,
                         phi ) );
    
end :
  CategoryGetters := rec( range_cat := RangeCategoryOfHomomorphismStructure ),
  CategoryFilter := HasRangeCategoryOfHomomorphismStructure,
  Description := "MorphismsOfExternalHom using MorphismsOfExternalHom in RangeCategoryOfHomomorphismStructure" );

##
AddDerivationToCAP( ListOfSubobjects,
  function( cat, A )
    local Omega, chis;
    
    Omega := SubobjectClassifier( cat );
    
    chis := MorphismsOfExternalHom( cat, A, Omega );
    
    return List( chis,
                 chi -> SubobjectOfClassifyingMorphism( cat, chi ) );
    
end );

##  A <-f-- X
##  |       |
##  |       |
##  x       g
##  |       |
##  v       v
##  D <-y-- B

##
AddDerivationToCAP( HasPushoutComplement,
  function( cat, f, x )
    local xx, x_i, Rel, A, Omega_A, ff, f_i, Omega_X, x_i_Omega_A, rpc, Omega_D, PC1, PC2;
    
    ## Implement condition (PC.1) in [Kawahara 90, Theorem 3.6]
    
    xx := AsMorphismInCategoryOfRelations( x );
    
    x_i := PseudoInverse( xx );
    
    Rel := CapCategory( x_i );
    
    A := Source( x );
    
    Omega_A := MaximalRelationIntoTerminalObject( A / Rel );
    
    ff := AsMorphismInCategoryOfRelations( f );
    
    f_i := PseudoInverse( ff );
    
    Omega_X := MaximalRelationIntoTerminalObject( Source( f ) / Rel );
    
    x_i_Omega_A := PreCompose( x_i, Omega_A );
    
    rpc := EmbeddingOfRelativePseudoComplementSubobject( ## this is the only line in which we need relativ pseudo-complements
                      EmbeddingOfRelationInDirectProduct( x_i_Omega_A ),
                      EmbeddingOfRelationInDirectProduct( PreCompose( [ x_i, f_i, Omega_X ] ) ) );
    
    Omega_D := MaximalRelationIntoTerminalObject( Range( x ) / Rel );
    
    PC1 :=
      IsEqualAsSubobjects(
              EmbeddingOfUnionSubobject( ## already exists in categories with coproducts
                      EmbeddingOfRelationInDirectProduct( x_i_Omega_A ), rpc ),
              EmbeddingOfRelationInDirectProduct( Omega_D ) );
    
    if PC1 = false then
        return false;
    fi;
    
    PC2 := IsDominating(
                   EmbeddingOfRelationInDirectProduct( PreCompose( xx, x_i ) ),
                   EmbeddingOfUnionSubobject( ## already exists in categories with coproducts
                           EmbeddingOfRelationInDirectProduct( AsMorphismInCategoryOfRelations( IdentityMorphism( A ) ) ),
                           EmbeddingOfRelationInDirectProduct( PreCompose( [ f_i, Omega_X, PseudoInverse( Omega_X ), ff ] ) )
                           ) );
    
    return PC2;
    
end );

##
AddDerivationToCAP( PushoutComplement,
  function( cat, f, x )
    local xx, x_i, Rel, A, Omega_A, ff, f_i, Omega_X, x_i_Omega_A, rpc, y, g;
    
    ## Construct the pushout-complement as in [Kawahara 90, Theorem 3.6]
    
    xx := AsMorphismInCategoryOfRelations( x );
    
    x_i := PseudoInverse( xx );
    
    Rel := CapCategory( x_i );
    
    A := Source( x );
    
    Omega_A := MaximalRelationIntoTerminalObject( A / Rel );
    
    ff := AsMorphismInCategoryOfRelations( f );
    
    f_i := PseudoInverse( ff );
    
    Omega_X := MaximalRelationIntoTerminalObject( Source( f ) / Rel );
    
    x_i_Omega_A := PreCompose( x_i, Omega_A );
    
    rpc := EmbeddingOfRelativePseudoComplementSubobject( ## this is the only line in which we need relativ pseudo-complements
                   EmbeddingOfRelationInDirectProduct( x_i_Omega_A ),
                   EmbeddingOfRelationInDirectProduct( PreCompose( [ x_i, f_i, Omega_X ] ) ) );
    
    y := PreCompose(
                 rpc,
                 ProjectionInFactorOfDirectProductWithGivenDirectProduct(
                         [ Range( x ), TerminalObject( cat ) ],
                         1,
                         Range( rpc ) ) );
    
    ## Debug
    Assert( 0, IsMonomorphism( y ) );
    
    y := ImageEmbedding( y );
    
    g := PreCompose( [ ff, xx, PseudoInverse( y ) ] );
    
    ## Debug
    Assert( 0, IsHonest( g ) );
    
    return [ HonestRepresentative( g ), y ];
    
end );

## Final derivations

##
AddFinalDerivation( CanonicalIdentificationFromImageObjectToCoimage,
                    [ [ ImageObject, 1 ],
                      [ IdentityMorphism, 1 ] ],
                    [ CanonicalIdentificationFromCoimageToImageObject,
                      CanonicalIdentificationFromImageObjectToCoimage,
                      CoimageObject,
                      CoimageProjection,
                      CoimageProjectionWithGivenCoimageObject,
                      AstrictionToCoimage,
                      AstrictionToCoimageWithGivenCoimageObject,
                      UniversalMorphismIntoCoimage,
                      UniversalMorphismIntoCoimageWithGivenCoimageObject,
                      IsomorphismFromCoimageToCokernelOfKernel,
                      IsomorphismFromCokernelOfKernelToCoimage ],
                    
  function( cat, mor )
    
    return IdentityMorphism( cat, ImageObject( cat, mor ) );
    
  end,
  [
    CanonicalIdentificationFromCoimageToImageObject,
    function( cat, mor )
    
      return IdentityMorphism( cat, ImageObject( cat, mor ) );
    
    end
  ] : CategoryFilter := HasIsElementaryTopos and IsElementaryTopos );
